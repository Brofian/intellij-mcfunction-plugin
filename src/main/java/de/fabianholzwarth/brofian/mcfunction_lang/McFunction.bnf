{
  parserClass="de.fabianholzwarth.brofian.mcfunction_lang.parser.McFunctionParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="McFunction"
  psiImplClassSuffix="Impl"
  psiPackage="de.fabianholzwarth.brofian.mcfunction_lang.psi"
  psiImplPackage="de.fabianholzwarth.brofian.mcfunction_lang.psi.impl"

  elementTypeHolderClass="de.fabianholzwarth.brofian.mcfunction_lang.psi.McFunctionTypes"
  elementTypeClass="de.fabianholzwarth.brofian.mcfunction_lang.psi.McFunctionElementType"
  tokenTypeClass="de.fabianholzwarth.brofian.mcfunction_lang.psi.McFunctionTokenType"

  psiImplUtilClass="de.fabianholzwarth.brofian.mcfunction_lang.psi.impl.McFunctionPsiImplUtil"

  tokens = [
     recoveryPoint="regexp:\R+"
  ]

}

// each file consists from an set of lines
mcFunctionFile ::= line_* // {recoverWhile=statement_recover}

// todo: recovery rule does not apply. The parsing is still stopped at the first error
// private statement_recover ::= !(CRLF|'\n'|COMMAND)

// each line can either be a comment, an empty line break, or a property with a line break
line_ ::= (property|COMMENT|CRLF)

// the available properties are the possible commands
property ::= (cmdAdvancement | cmdAttribute | cmdBan | cmdBossBar | cmdClear | cmdClone | cmdDamage | cmdData | cmdDataPack | cmdDebug | cmdDefaultGameMode |
              cmdDifficulty | cmdEffect | cmdEnchant | cmdExperience | cmdFill | cmdFillBiome | cmdForceLoad | cmdFunction | cmdGameRule | cmdGameMode | cmdGive |
              cmdHelp | cmdItem | cmdJfr | cmdKick | cmdKill | cmdList | cmdLocate | cmdLoot | cmdMe | cmdMsg | cmdOp | cmdPardon | cmdPardonIp | cmdParticle |
              cmdPlace | cmdSetBlock ) {
  mixin="de.fabianholzwarth.brofian.mcfunction_lang.psi.impl.McFunctionNamedElementImpl"
  implements="de.fabianholzwarth.brofian.mcfunction_lang.psi.McFunctionNamedElement"
  methods=[getKey getValue getName setName getNameIdentifier]
}

// command argument and command are two types used in the lexer, but are unused. Instead, the command names are used separately here
placeholder ::= COMMAND_ARGUMENT | COMMAND
unused ::= OPERATOR

// shortcuts for frequently used sequences
coordinateTriple ::= ((COORDINATE|NUMBER) (COORDINATE|NUMBER) (COORDINATE|NUMBER))
targetSelector ::= (SELECTOR|IDENTIFIER)
extendedIdentifier ::= (IDENTIFIER[JSON])
// todo: prevent space between identifier and json


// the command definitions
cmdAdvancement ::= ('advancement' ('revoke'|'grant') targetSelector ('everything' |
                                                                     (('from'|'through'|'until') IDENTIFIER) |
                                                                     ('only' IDENTIFIER [ IDENTIFIER ]))) 
cmdAttribute ::= ('attribute' targetSelector IDENTIFIER (('get' NUMBER) |
                                                         ('base' ('get' [NUMBER] |'set' NUMBER)) |
                                                         ('modifier' (
                                                            ('add' IDENTIFIER IDENTIFIER NUMBER ('add'|'multiply'|'multiply_base')) |
                                                            ('remove' IDENTIFIER) |
                                                            ('value' 'get' IDENTIFIER [NUMBER]))))) 
cmdBan ::= ('ban' targetSelector [STRING]) 
cmdBossBar ::= ('bossbar' (('add' IDENTIFIER STRING) |
                           ('get' IDENTIFIER ('max'|'players'|'value'|'visible')) |
                           ('list') |
                           ('remove' IDENTIFIER) |
                           ('set' IDENTIFIER (
                                ('color' COLOR) |
                                ('max' NUMBER) |
                                ('name' STRING) |
                                ('players' targetSelector) |
                                ('style' ('notched_6'|'notched_10'|'notched_12'|'notched_20'|'progress')) |
                                ('value' NUMBER) |
                                ('visible' ('true'|'false'))
                           )))) 
cmdClear ::= ('clear' [SELECTOR [extendedIdentifier [NUMBER]]])
cmdClone ::= ('clone' ['from' IDENTIFIER] coordinateTriple coordinateTriple
                      ['to' IDENTIFIER]   coordinateTriple
                      [ (('replace'|'masked') ['force'|'move'|'normal']) |
                        ('filtered' IDENTIFIER ['force'|'move'|'normal'])
                      ]) 
cmdDamage ::= ('damage' targetSelector NUMBER [IDENTIFIER [('at' coordinateTriple) |
                                                           ('by' IDENTIFIER ['from' IDENTIFIER])]]) 
cmdData ::= ('data' (('get' cmdData_Target [IDENTIFIER [NUMBER]]) |
                     ('merge' cmdData_Target JSON) |
                     ('modify' cmdData_Target STRING ('append'|('insert' NUMBER)|'merge'|'prepend'|'set') (
                        ('from' cmdData_Target STRING) |
                        ('string' cmdData_Target STRING NUMBER NUMBER) |
                        ('value' (NUMBER|STRING|JSON|IDENTIFIER)) |
                     )) |
                     ('remove' cmdData_Target STRING)))
    cmdData_Target ::= ('block' coordinateTriple) |
                       ('entity' targetSelector) |
                       ('storage' STRING)
cmdDataPack ::= ('datapack' (('disable' IDENTIFIER) |
                             ('enable' IDENTIFIER [('first'|'last') | (('before'|'after') IDENTIFIER)]) |
                             ('list' ['available'|'enabled'])))
cmdDebug ::= ('debug' ('start' | 'stop' | ('function' IDENTIFIER)))
cmdDefaultGameMode ::= ('defaultgamemode' ('survival'|'creative'|'adventure'|'spectator'))
cmdDifficulty ::= ('difficulty' ['peaceful'|'easy'|'normal'|'hard'])
cmdEffect ::= ('effect' (('clear' [targetSelector [IDENTIFIER]]) |
                         ('give' targetSelector IDENTIFIER [(NUMBER|'infinite') [NUMBER ['true'|'false']]])))
cmdEnchant ::= ('enchant' targetSelector IDENTIFIER [NUMBER])
cmdExperience ::= (('enchant'|'xp') (('query' targetSelector ('levels'|'points')) |
                                     (('add'|'set') targetSelector ['levels'|'points'])))
cmdFill ::= ('fill' coordinateTriple coordinateTriple extendedIdentifier [ 'destroy'|'hollow'|'keep'|'outline' | ('replace' IDENTIFIER) ])
cmdFillBiome ::= ('fillbiome' coordinateTriple coordinateTriple IDENTIFIER ['replace' IDENTIFIER])
cmdForceLoad ::= ('forceload' (('add' COORDINATE COORDINATE [COORDINATE COORDINATE]) |
                               ('remove' ('all'| (COORDINATE COORDINATE [COORDINATE COORDINATE]))) |
                               ('query' COORDINATE COORDINATE)))
cmdFunction ::= ('function' IDENTIFIER)
cmdGameMode ::= ('gamemode' ('survival'|'creative'|'adventure'|'spectator') [targetSelector])
cmdGameRule ::= ('gamerule' IDENTIFIER [NUMBER|'true'|'false'])
cmdGive ::= ('give' targetSelector extendedIdentifier [ NUMBER ] )
cmdHelp ::= ('help')
cmdItem ::= ('item' (('modify' cmdItem_Target IDENTIFIER IDENTIFIER) |
                     ('replace' cmdItem_Target IDENTIFIER (('with' extendedIdentifier NUMBER) |
                                                           ('from' cmdItem_Target IDENTIFIER IDENTIFIER)))))
        cmdItem_Target ::= (('block' coordinateTriple) | ('entity' targetSelector))
cmdJfr ::= ('jfr' ('start'|'stop'))
cmdKick ::= ('kick' targetSelector [STRING])
cmdKill ::= ('kill' targetSelector)
cmdList ::= ('list' ['uuids'])
cmdLocate ::= ('locate' ('structure'|'biome'|'poi') IDENTIFIER)
cmdLoot ::= ('loot' cmdLoot_Target cmdLoot_Source)
        cmdLoot_Target ::= (('give' targetSelector) |
                            ('insert' coordinateTriple) |
                            ('spawn' coordinateTriple) |
                            ('replace' (('block' coordinateTriple IDENTIFIER [NUMBER]) |
                                        ('entity' targetSelector IDENTIFIER [NUMBER]))))
        cmdLoot_Source ::= (('fish' IDENTIFIER coordinateTriple [extendedIdentifier|'mainhand'|'offhand']) |
                            ('loot' IDENTIFIER) |
                            ('kill' targetSelector) |
                            ('mine' coordinateTriple [extendedIdentifier|'mainhand'|'offhand']))
cmdMe ::= ('me' IDENTIFIER*)
cmdMsg ::= ('msg' targetSelector IDENTIFIER*)
cmdOp ::= ('op' targetSelector)
cmdPardon ::= ('pardon' targetSelector)
cmdPardonIp ::= ('pardon-ip' IDENTIFIER)
cmdParticle ::= ('particle' IDENTIFIER [coordinateTriple [coordinateTriple NUMBER NUMBER [('force'|'normal') [targetSelector]]]])
cmdPlace ::= ('place' (('feature' IDENTIFIER [coordinateTriple]) |
                       ('jigsaw' IDENTIFIER IDENTIFIER NUMBER [coordinateTriple]) |
                       ('structure' IDENTIFIER [coordinateTriple]) |
                       ('template' IDENTIFIER [coordinateTriple [('none'|'clockwise_90'|'counterclockwise_90'|'180') [('none'|'front_back'|'left_right') [NUMBER [NUMBER]]]]])))

cmdSetBlock ::= ('setblock' coordinateTriple IDENTIFIER)


